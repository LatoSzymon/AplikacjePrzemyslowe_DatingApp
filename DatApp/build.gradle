plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'AplikacjePrzemyslowe'
version = '0.0.1-SNAPSHOT'
description = 'Dating app na aplikacje przemysłowe'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
    // --- APLIKACJA ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'

    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.modelmapper:modelmapper:3.2.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.15.2'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // --- LOMBOK ---
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // --- TESTY ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'

    // Testcontainers
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mariadb'

    // Selenium / architektura
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.38.0'
    testImplementation 'com.tngtech.archunit:archunit:1.4.1'
}


jar {
    enabled = false
}
bootJar {
    enabled = true
}

tasks.named('test') {
    useJUnitPlatform()
    // Na czas pracy nad testami zostawiamy możliwość generowania raportu JaCoCo mimo błędów.
    // Gdy dojdziemy do zielonego builda, ustawimy to z powrotem na false.
    ignoreFailures = false
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.75
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
